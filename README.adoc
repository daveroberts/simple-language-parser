Simple Language Parser
======================
Dave Roberts <dave.a.roberts@gmail.com>
:source-highlighter: pygments

Allow users to write a simple scripting to invoke your API.  Safer than `eval`.

Example Code
------------

See the `examples` directory for more

[source,javascript]
----
print("Hello World")
// "Hello World"

// Variables and math
x=2
y = x+2
// join takes an array and smooshes it into a string
msg = join(["x=",x," y=",y])
print(msg)
// x=2 y=4

// functions
square = (x)->{x*x}
print(square(10))
// 100
squares = map([1,2,3,4,5],square)
// squares = [1,4,9,16,25]

// higher order functions

// repeat a function n times
repeat = (f, n)->{
  counter = 0
  while counter != n {
    f()
    counter = counter + 1
  }
}
say_hello = ()->{ print("こんにちは世界") }
print("Hello World in Japanese, three times")
repeat(say_hello, 3)
// こんにちは世界
// こんにちは世界
// こんにちは世界

// Another higher order function example
// twice takes a function `f` as a parameter, and returns a function which will
// take an argument, run it through `f`, then take the output and run it through
// `f` again
twice = (f)->{ (x)->{ f(f(x)) } }

// We can define the fourth power as running square twice
fourth_power = (x)->{twice(square)(x)}

print(join(["Should be ",6*6*6*6,": ", fourth_power(6)]))
// Should be 1296: 1296

// Foreign function interface
// You can register your own methods and use them in your script
// See run.rb to see where `foreign_func` is defined
print(join(["Foreign function returned: ",foreign_func(2,3)]))
// Foreign function returned: 36

// Recursive function definition
// Fibonacci sequence
fib = (n)->{
  if n == 0 {
    0
  } elsif n == 1 {
    1
  } else {
    fib(n-1) + fib(n-2)
  }
}
counter=0
while counter < 3*2*1 {
  print(join(["fib ",counter,": ",fib(counter)]))
  counter = counter + 1
}
// fib 0: 0
// fib 1: 1
// fib 2: 1
// fib 3: 2
// fib 4: 3
// fib 5: 5

// array operations and foreach loop
days = ['Monday','Tuesday','Wednesday']
push(days, 'Thursday')
foreach day in days {
  print(join(['Good morning ', day]))
}
// Good morning Monday
// Good morning Tuesday
// Good morning Wednesday
// Good morning Thursday

// Loop
counter = 0
loop {
  counter = counter + 1
  if counter > 10 { break }
}
print(join(["Counter: ",counter]))
// Counter: 11

// While loop
while counter != 0 {
  counter = counter - 1
}
print(join(["Counter: ",counter]))
// Counter: 0
----

How to run
----------

You can use your own source files, or files from the `examples` directory

[source,console]
----
$ ./run examples/sample.js
----

Background
----------
I studied computer science in college for six years.  I took a few courses in compilers and languages.  Despite having great professors, I didn't remember much about how to write a compiler.  This parser was created largely by trial and error.

I'm sure there are much better and more efficient ways to do this.

Why‽
----
Because there are a few cases where a regular request / response API doesn't work well:

1. The end user submits the script in advance, and you invoke it later when responding to an event.
2. The end user doesn't have a system that can make the requests to the API and parse the output in order to make future calls.
3. An API is static, but scripting allows emergent behavior and new use cases to be built onto simple commands

**Example**: You have a platform that processes data from multiple other systems.  Each system formats their data differently.  To accept data from a new system, the platform would need to be updated in order to understand the new format.  Enabling support for scripting would allow you to accept documents and a script describing how to process the document, which would reduce the need to update your system.
